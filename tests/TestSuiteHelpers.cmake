set(OMNETPP_CMAKE_ROOT ${CMAKE_CURRENT_LIST_DIR}/..)

macro(require_envvar)
    set(envvar_list "${ARGN}")
    foreach(envvar IN LISTS envvar_list)
        if(NOT DEFINED ENV{${envvar}})
            message(FATAL_ERROR "require_envvar: ${envvar} is missing")
        endif()
    endforeach()
endmacro()

macro(expect_strequal expected actual)
    if(NOT DEFINED ${actual})
        message(FATAL_ERROR "expect_strequal: variable ${actual} does not exist")
    endif()
    if(NOT "${expected}" STREQUAL "${${actual}}")
        message(FATAL_ERROR "Expected ${actual} to equal \"${expected}\", but it is \"${${actual}}\"")
    endif()
endmacro()

macro(expect_target target)
    if(NOT TARGET ${target})
        message(FATAL_ERROR "expect_target: target ${target} does not exist")
    endif()
endmacro()

macro(expect_command command)
    if(NOT COMMAND ${command})
        message(FATAL_ERROR "expect_command: command ${command} does not exist")
    endif()
endmacro()
